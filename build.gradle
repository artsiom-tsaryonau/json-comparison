buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
        classpath "org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:2.1.1"
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'
    apply plugin: "org.unbroken-dome.test-sets"

    group = 'com.assignment.diff.jsoncomparison'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion '8.22'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        sourceSets = [project.sourceSets.main]
    }

    pmd {
        ignoreFailures = true
    }

    spotbugs {
        sourceSets = [sourceSets.main]
    }

    spotbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    testSets {
        testInteg
    }

    jacocoTestReport {
        afterEvaluate {
            classDirectories.setFrom files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['**/dto/**'])
            })
        }
    }

    testInteg.mustRunAfter test
    testInteg.finalizedBy jacocoTestReport
    check.dependsOn testInteg

    dependencies {
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"

        testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
        testImplementation "org.easymock:easymock:$easymockVersion"

        runtimeOnly "com.h2database:h2:$h2Version"
    }
}
