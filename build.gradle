buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
        classpath "org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:2.1.1"
        classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.4.0'
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'com.github.spotbugs'
    apply plugin: "org.unbroken-dome.test-sets"
    apply plugin: "info.solidsoft.pitest"

    group = 'com.assignment.diff.jsoncomparison'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java-library'

    repositories {
        mavenCentral()
    }

    checkstyle {
        toolVersion '8.22'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        sourceSets = [project.sourceSets.main]
    }

    pmd {
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    spotbugs {
        sourceSets = [sourceSets.main]
    }

    spotbugsMain {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    testSets {
        testInteg
    }

    pitest {
        targetClasses = ['com.assignment.diff.jsoncomparison.*']
        pitestVersion = '1.4.9'
        threads = 6
        outputFormats = ['HTML']
        
        if (project.name in [
                'json-comparison-application',
                'json-comparison-domain',
                'json-comparison-repository'
        ]) {
            failWhenNoMutations = false
        }
    }

    jacocoTestReport {
        afterEvaluate {
            classDirectories.setFrom files(classDirectories.files.collect {
                fileTree(dir: it, exclude: ['**/dto/**'])
            })
        }
    }

    testInteg.mustRunAfter test
    testInteg.finalizedBy jacocoTestReport
    check.dependsOn testInteg

    dependencies {
        // split dependencies
        implementation "org.apache.commons:commons-lang3:$commonsLangVersion"
        testImplementation "org.easymock:easymock:$easymockVersion"

        implementation "org.springframework.boot:spring-boot-starter-webflux:$springBootVersion"
        implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        // reactive repositories
        implementation 'org.springframework.data:spring-data-r2dbc:1.0.0.RELEASE'
        implementation 'io.r2dbc:r2dbc-h2:0.8.1.RELEASE'

        testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
        testImplementation "io.projectreactor:reactor-test:3.3.1.RELEASE"
    }
}
